/*
 * Put your own Gradle tasks here
 */

task tomcatInstallShared() {
    group 'Tomcat'
    description 'Add shared.loader dependencies'
    dependsOn ':portalProperties'

    doLast {
        String serverHome = rootProject.ext['buildProperties'].getProperty('server.home')

        File tomcatSharedDir = new File("${serverHome}/shared/lib")

        tomcatSharedDir.deleteDir()

        copy {
            from configurations.shared
            into tomcatSharedDir
        }
    }
}

task mycppPackage(type: Tar) {
    group 'MyCPP'
    description 'Create a .tar.gz file with uPortal and portlets'
    dependsOn ':portalProperties'
    dependsOn ':configureMyCPPTar'
    mustRunAfter ':tomcatInstall'
    mustRunAfter allprojects.collect { it.tasks.matching { it.name.equals('tomcatDeploy') } }

    destinationDir(new File("${rootProject.projectDir}"))

    baseName 'mycpp'
    extension 'tar.gz'

    compression Compression.GZIP
    includeEmptyDirs false
}

task configureMyCPPTar {
    dependsOn ':portalProperties'
    mustRunAfter ':tomcatInstall'
    mustRunAfter allprojects.collect { it.tasks.matching { it.name.equals('tomcatDeploy') } }

    doLast {
        String serverBase = rootProject.ext.buildProperties.getProperty('server.base')

        configure(tasks.'mycppPackage') {
            from("${serverBase}") {
                into 'tomcat'

                exclude 'logs'
                exclude 'temp'
                exclude 'work'
                exclude 'webapps/docs'
                exclude 'webapps/examples'
                exclude 'webapps/host-manager'
                exclude 'webapps/manager'
                exclude 'webapps/ROOT'
            }
        }
    }
}
